* Emacs Literate Config
** Notes
After executing tangle, copy the output files to the target
directory and if adding a new section make sure to update
the init.el file in the target directory:

cp -r ./modules/* ~/.config/[target-env]/modules/

Example

cp -r ~/projects/emacs_babel_config/modules/* ~/.config/emacs-prod/modules/
   
* Environment

Defines external environment variables and configurations used across modules.
Manually modify the `active-env` block below to switch between prod/dev environments.

Options emacs-prod | emacs-dev
#+NAME: target-env
#+BEGIN_SRC emacs-lisp :results value
  (format "\"%s\"" "emacs-prod")
#+END_SRC

#+RESULTS: target-env
: "emacs-prod"

Set target os.

Options archlinux | windows
#+NAME: target-os
#+BEGIN_SRC emacs-lisp :results value
  (format "\"%s\"" "archlinux")  
#+END_SRC

#+RESULTS: target-os
: "archlinux"

Variable definitions for those variables that can shared across all
modules and enviroments. These should be used judiciously.
All other variable definitions should be within their respective code blocks.
#+NAME: emacs-common-dir
#+BEGIN_SRC emacs-lisp :results value
  (format "\"%s\"" "~/.config/emacs-common")
#+END_SRC

#+RESULTS: emacs-common-dir
: "~/.config/emacs-common"

Set evnrionment variables.
#+BEGIN_SRC emacs-lisp :noweb yes  :tangle ./modules/env.el
  (setq my/env <<target-env()>>)
  (setq server-name <<target-env()>>) 
#+END_SRC

#+RESULTS:
: emacs-prod

Load my API keys.
#+BEGIN_SRC emacs-lisp :noweb yes  :tangle ./modules/env.el
  (load-file (concat <<emacs-common-dir()>> "/api-keys.el"))
#+END_SRC

#+RESULTS:
: t

Starts emacs in server mode.
#+BEGIN_SRC emacs-lisp  :eval never :tangle ./modules/env.el
  (require 'server)
  (unless (server-running-p)
     (server-start))
#+END_SRC

Turn off yes/no prompt for evaluating org buffer.
#+BEGIN_SRC emacs-lisp  :eval never :tangle ./modules/env.el
  ;; Turn of eval protection
  (setq org-confirm-babel-evaluate nil)
#+END_SRC

Emacs Custom File
#+BEGIN_SRC emacs-lisp :tangle ./modules/env.el
  (setq custom-file (expand-file-name "custom.el" user-emacs-directory))
  (load custom-file 'noerror)
#+END_SRC

#+RESULTS:
: t

* Core

Core emacs configurtation. These setting should be the same across any of my hardware / os
environments (e.g. laptop, desktop, pi).
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/core.el

  ;; Start quiet
  (setq inhibit-startup-screen t
        inhibit-startup-message t
        ring-bell-function #'ignore)

  ;; Files/backups
  (setq make-backup-files nil
        auto-save-default nil
        load-prefer-newer t)

  ;; UTF-8 everywhere
  (prefer-coding-system 'utf-8)
  (set-default-coding-systems 'utf-8)
  (set-terminal-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)

  ;; Make ESC quit prompts
  (global-set-key (kbd "<escape>") #'keyboard-escape-quit)

  ;; Don’t let package.el auto-enable itself (we use straight.el)
  (setq package-enable-at-startup nil)


  ;; straight.el bootstrap + use-package integration
  (defvar bootstrap-version)
  (let* ((user-dir user-emacs-directory)
         (bootstrap-file
          (expand-file-name "straight/repos/straight.el/bootstrap.el" user-dir)))
    (unless (file-exists-p bootstrap-file)
      (with-current-buffer
          (url-retrieve-synchronously
           "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
           'silent 'inhibit-cookies)
        (goto-char (point-max))
        (eval-print-last-sexp)))
    (load bootstrap-file nil 'nomessage))

  ;; Install use-package via straight and make it the default installer
  (straight-use-package 'use-package)
  (setq straight-use-package-by-default t)
  (require 'use-package)

  ;; Housekeeping
  (use-package no-littering
    :config
    (setq auto-save-file-name-transforms
          `((".*" ,(no-littering-expand-var-file-name "auto-save/") t))))

  ;; Optional: Weekly straight update + lock versions at 09:00
  (add-hook 'emacs-startup-hook
            (lambda ()
              (run-at-time "09:00" (* 7 24 60 60)
                           (lambda ()
                             (message "Straight: pulling all & freezing versions…")
                             (straight-pull-all)
                             (straight-freeze-versions)
                             (message "Straight: done.")))))

  ;; Ensure environment variables inside Emacs look the same as in the shell.
  (use-package exec-path-from-shell
    :init)
  (when (memq window-system '(mac ns x))
    (exec-path-from-shell-initialize))
  (when (daemonp)
    (exec-path-from-shell-initialize))

  (provide 'core)
#+END_SRC

* Core-Extensions

Core extenstions emacs configuration. Ideally these will be the same across all environments.
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/core-extensions.el
  ;; Enable which-key for keybinding discovery
  (use-package which-key
    :defer 0
    :diminish
    :config
    (which-key-mode))

  ;; Completion and search system
  (use-package ivy
    :diminish
    :bind (("C-s" . swiper)
           :map ivy-minibuffer-map
           ("TAB" . ivy-alt-done)
           ("C-l" . ivy-alt-done)
           ("C-j" . ivy-next-line)
           ("C-k" . ivy-previous-line)
           :map ivy-switch-buffer-map
           ("C-k" . ivy-previous-line)
           ("C-l" . ivy-done)
           ("C-d" . ivy-switch-buffer-kill)
           :map ivy-reverse-i-search-map
           ("C-k" . ivy-previous-line)
           ("C-d" . ivy-reverse-i-search-kill))
    :config
    (ivy-mode 1))

  (use-package counsel
    :bind (("C-M-j" . counsel-switch-buffer)
           :map minibuffer-local-map
           ("C-r" . counsel-minibuffer-history))
    :custom
    (counsel-linux-app-format-function #'counsel-linux-app-format-function-name-only)
    :config
    (counsel-mode 1))

  (use-package ivy-rich
    :after counsel
    :init
    (ivy-rich-mode 1))

  ;; Use to prioritize command history based on usage.
  (use-package ivy-prescient
  :after counsel
  :custom
  (ivy-prescient-enable-filtering nil)
  :config
  ;; Uncomment the following line to have sorting remembered across sessions!
  ;: (prescient-persist-mode 1)
  (ivy-prescient-mode 1))

  (use-package helpful
    :custom
    (counsel-describe-function-function #'helpful-callable)
    (counsel-describe-variable-function #'helpful-variable)
    :bind
    ([remap describe-function] . counsel-describe-function)
    ([remap describe-command] . helpful-command)
    ([remap describe-variable] . counsel-describe-variable)
    ([remap describe-key] . helpful-key))
#+END_SRC

* System & OS Integration

System / OS integration. These could change between environments.
#+BEGIN_SRC emacs-lisp :results value :noweb yes
  ;; Unhide dotfiles.
  (cond
   ((string= <<target-os()>> "archlinux")
    "(setq dired-listing-switches \"-alh --group-directories-first\")")
   ((string= <<target-os()>> "macos")
    "(setq dired-listing-switches \"-alh\")")
   (t "Unknown OS"))
#+END_SRC

#+RESULTS:
: (setq dired-listing-switches "-alh --group-directories-first")

Dired configurations.
#+BEGIN_SRC emacs-lisp :eval never :noweb yes :tangle ./modules/system-os.el
  (use-package dired
    :straight (:type built-in)
    :ensure nil
    :commands (dired dired-jump)
    :bind (("C-x C-j" . dired-jump)))

  (use-package all-the-icons-dired
    :hook (dired-mode . all-the-icons-dired-mode))

  ;; Launch apps based on content.
  (use-package dired-open
    :config
    (setq dired-open-extensions
  	'(("png" . "imv")
  	  ("jpg" . "imv")
  	  ("pdf" . "zathura")
  	  ("mp4" . "mpv")
  	  ("mkv" . "mpv")
  	  ("html" . "floorp"))))

  ;; Bind enter to launch associated file app.
  (with-eval-after-load 'dired
  ;; Replace RET behavior
  (define-key dired-mode-map (kbd "RET") #'dired-open-file))


  (use-package dired-hide-dotfiles
    :hook (dired-mode . dired-hide-dotfiles-mode))

  ;; Must have dired extensions
  (use-package peep-dired
    :ensure t
    :bind (:map dired-mode-map
                ("P" . peep-dired))
    :hook (peep-dired-mode . (lambda () (setq-local image-dired-display-image-buffer 'other))))

  (use-package dired-subtree
    :ensure t
    :bind (:map dired-mode-map
                ("<tab>" . dired-subtree-toggle)))
#+END_SRC

Terminal
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/system-os.el
  (use-package vterm
    :commands vterm
    :bind ("C-c v" . vterm)
    :config
    (setq vterm-shell "/usr/bin/fish")
    (setq vterm-max-scrollback 10000))
#+END_SRC

* Remote Connections

Connections to my remote machines.
#+BEGIN_SRC emacs-lisp :eval never :results silent :tangle ./modules/remote.el
  ;;Debug statements
  ;;(setq tramp-verbose 10)
  ;;(setq tramp-debug-buffer t)

  (defun remote/dired-pi-5 ()
    "Open Dired in home directory on pi-5."
    (interactive)
    (dired "/ssh:username@192.168.1.57:/home/username/"))

  (defun remote/dired-lenovo ()
    "Open Dired in home directory on lenovo."
    (interactive)
    (dired "/ssh:username@192.168.1.80:/home/username/"))

  (defun remote/dired-dell ()
    "Open Dired in home directory on dell."
    (interactive)
    (dired "/ssh:username@192.168.1.108:/home/username/"))
#+END_SRC

* UI

Emacs configuration for usability experience an QOL.
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/ui.el
    ;; Font sizing defaults for UI scaling (override per-host if needed)
    (defvar my/default-font-size 100)
    (defvar my/default-variable-font-size 100)

    ;; Frame transparency defaults
    (defvar my/frame-transparency '(90 . 90))

    ;; Disable unnecessary UI elements
    (scroll-bar-mode -1)
    (tool-bar-mode -1)
    (tooltip-mode -1)
    (menu-bar-mode -1)
    (set-fringe-mode 10)

    ;; Set up the visible bell
    (setq visible-bell t)

    ;; Show column and line numbers
    (column-number-mode)
    (global-display-line-numbers-mode t)

    ;; Set frame font and theme
    (set-face-attribute 'default nil :font "JetBrains Mono" :height my/default-font-size)
    (set-face-attribute 'fixed-pitch nil :font "Fira Code Retina" :height my/default-font-size)
    (set-face-attribute 'variable-pitch nil :font "Cantarell" :height my/default-variable-font-size :weight 'regular)

    ;; Apply frame transparency
    (set-frame-parameter (selected-frame) 'alpha my/frame-transparency)
    (add-to-list 'default-frame-alist `(alpha . ,my/frame-transparency))

    ;; Themes
    (use-package spacemacs-theme :defer t)
    (use-package doom-themes :defer t)
    (use-package modus-themes :defer t)

    (load-theme 'doom-1337 t)

    (use-package doom-modeline
      :after (nerd-icons)
      :config
      (setq doom-modeline-minor-modes t)
      (setq doom-modeline-major-mode-icon t)
      (setq doom-modeline-enable-word-count t)
      (setq doom-modeline-height 30)
      (setq doom-modeline-bar-width 5)
      (setq doom-modeline-indent-info t)
      (setq doom-modeline-lsp t)
      (setq doom-modeline-github t)
      (setq doom-modeline-buffer-modification-icon t)
      (setq doom-modeline-unicode-fallback t)
      :hook (after-init . doom-modeline-mode))

       ;; Focus follows mouse
    (setq mouse-autoselect-window t)

      ;; Setup window borders like wtm
    (window-divider-mode 1)
    (setq window-divider-default-places t)
    (setq window-divider-default-bottom-width 1)
    (setq window-divider-default-right-width 1)

     ;; Set all borders to orange
    (set-face-attribute 'window-divider nil :foreground "orange")
    (set-face-attribute 'vertical-border nil :foreground "orange")

    ;; Mode line borders - also orange
    (set-face-attribute 'mode-line nil
                      :background "#4c566a"
                      :foreground "#eceff4"
                      :box '(:line-width 1 :color "orange"))

    (set-face-attribute 'mode-line-inactive nil
                      :background "#2e3440"
                      :foreground "#88909f"
                      :box '(:line-width 1 :color "orange"))

  ;; Window shading - active window much darker
    (defvar my-active-window-background "#000000")    ; Very dark for active
    (defvar my-inactive-window-background "#2a2a2a")  ; Lighter for inactive

    (defun my-apply-window-shading ()
    "Apply shading - active window darker, inactive lighter."
       (dolist (window (window-list))
         (with-current-buffer (window-buffer window)
           (face-remap-reset-base 'default)
           (if (eq window (selected-window))
               ;; Active window - much darker
               (face-remap-add-relative 'default :background my-active-window-background)
             ;; Inactive windows - lighter
             (face-remap-add-relative 'default :background my-inactive-window-background)))))

    ;; Apply shading on window changes
    (add-hook 'window-selection-change-functions 
            (lambda (&rest _) (my-apply-window-shading)))

    ;; Protect settings from being overridden
    (defun my-protect-window-settings (&rest _)
       (when window-divider-mode
         (setq window-divider-default-bottom-width 1)
         (setq window-divider-default-right-width 1))
       (set-face-attribute 'window-divider nil :foreground "orange")
       (set-face-attribute 'vertical-border nil :foreground "orange")
       (my-apply-window-shading))

    (advice-add 'load-theme :after #'my-protect-window-settings)

    ;; Initialize everything
    (my-apply-window-shading)

    ;; End of Window Configuration
    (put 'erase-buffer 'disabled nil)

    ;; Show line numbers:
    (add-hook 'prog-mode-hook 'display-line-numbers-mode)
    (add-hook 'text-mode-hook 'display-line-numbers-mode)
    (global-set-key (kbd "<f9>") 'display-line-numbers-mode)

    ;; Show parent parenthesis.
    (show-paren-mode 1)

    ;; Setup smooth scrolling.
    (setq scroll-conservatively 1)

    ;; Switch cursor to new window automatically
    (defun split-and-follow-horizontally ()
      (interactive)
      (split-window-below)
      (balance-windows)
      (other-window 1))
    (global-set-key (kbd "C-x 2") 'split-and-follow-horizontally)

    (defun split-and-follow-vertically ()
      (interactive)
      (split-window-right)
      (balance-windows)
      (other-window 1))
    (global-set-key (kbd "C-x 3") 'split-and-follow-vertically)  

    ;; Highlight current line.  
    (add-hook 'after-init-hook 'global-hl-line-mode)

    ;; Bracket pair-matching.
    (setq electric-pair-pairs '(
                            (?\{ . ?\})
                            (?\( . ?\))
                            (?\[ . ?\])
                            (?\" . ?\")
                            ))
    (electric-pair-mode t)

    ;; Clean up minor mode with minions.
    (use-package minions
    :config (minions-mode 1)
    (setq minions-mode-line-lighter "☰"))

    ;; Icons on Emacs.
    (use-package nerd-icons
      :custom
      (nerd-icons-scale-factor 1.0)
      (nerd-icons-default-adjust 0.0))

    (use-package nerd-icons-completion      :straight
      (nerd-icons-completion :type git :host github
                         :repo "rainstormstudio/nerd-icons-completion")
      :demand t
      :hook
      (marginalia-mode . nerd-icons-completion-marginalia-setup)
      :config
      (nerd-icons-completion-mode))

    (use-package nerd-icons-dired
      :straight (nerd-icons-dired :type git :host github
                              :repo "rainstormstudio/nerd-icons-dired")
      :hook
      (dired-mode . nerd-icons-dired-mode))

    (use-package treemacs-nerd-icons
      :straight (treemacs-nerd-icons :type git :host github
                                 :repo "rainstormstudio/treemacs-nerd-icons")
      :config
      (with-eval-after-load 'treemacs
        (treemacs-load-theme "nerd-icons")))

    ;; Better undo + redo
    (use-package undo-tree
      :config
      (global-undo-tree-mode 1))

    ;; Briefly highlight the cursor when switching windows/buffers.
    (use-package beacon
      :init
      (beacon-mode 1))

    ;; Hightlight, index and go to any character by pressing the index key.
    (use-package avy
      :bind
      ("M-s" . avy-goto-char))

    ;; Shows window numbers to select to change window
    (use-package ace-window
    :ensure t
    :bind (("M-o" . ace-window)))

    ;; Better way to switch windows.
    (use-package switch-window
      :config
      (setq switch-window-input-style 'minibuffer)
      (setq switch-window-increase 4)
      (setq switch-window-threshold 2)
      (setq switch-window-shortcut-style 'qwerty)
      (setq switch-window-qwerty-shortcuts
  	  '("a" "s" "d" "f" "j" "k" "l"))
      (setq switch-window-minibuffer-shortcut ?z)
      :bind
      ([remap other-window] . switch-window))

      ;; Display page breaks as horizontal lines.
      (use-package page-break-lines
        :requires dashboard
        :init
        (global-page-break-lines-mode))
 #+END_SRC

* Org

Dedicated section for Org due to its scope of potential use and integration with emacs native.

Random notes, commands, quotes, etc. file.
#+NAME: random-notes-dir
#+BEGIN_SRC emacs-lisp :results value
  (format "\"%s\"" "~/Documents/stuff/random_notes.org")
#+END_SRC

#+RESULTS: random-notes-dir
: "~/Documents/stuff/random_notes.org"

Random notes function.
#+BEGIN_SRC emacs-lisp :eval never :noweb yes :tangle ./modules/org.el
;; Tell straight not to fetch/build Org; use Emacs' built-in instead.
  (use-package org
    :straight (:type built-in))
  
  (use-package org
         :defer t )
   
   (setq my/random-notes-file <<random-notes-dir()>>)

     ;; Org Capture Template
     (setq org-capture-templates
           '(("r" "Random quick note"
              entry
              (file+headline my/random-notes-file "Inbox")
              "* %U %?\n  :tags: %^{Tags}\n"
              :empty-lines 1)))
     (global-set-key (kbd "C-c r") 'org-capture)
 #+END_SRC

 Enable python code blocks in babel.
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/org.el
 (org-babel-do-load-languages
 'org-babel-load-languages
 '((python . t)))
 #+END_SRC
 
* Email

Dedicated section for email due to its scope of potential use and integration with emacs native.
#+NAME: yahoo-email-dir
#+BEGIN_SRC emacs-lisp :results value
  (format "\"%s\"" "~/Maildir/yahoo")
#+END_SRC

#+RESULTS: yahoo-email-dir
: "~/Maildir/yahoo"

#+NAME: yahoo-email-address
#+BEGIN_SRC emacs-lisp :results value
  (format "\"%s\"" "mkburns61@yahoo.com")    
#+END_SRC

#+RESULTS: yahoo-email-address
: "mkburns61@yahoo.com"

#+NAME: yahoo-email-fullname
#+BEGIN_SRC emacs-lisp :results value
  (format "\"%s\"" "Mike Burns")    
#+END_SRC

#+RESULTS: yahoo-email-fullname
: "Mike Burns"

Big brother contacts database location.
#+NAME: bbdb-dir
#+BEGIN_SRC emacs-lisp :noweb yes :results value
  (format "\"%s\"" (concat <<emacs-common-dir()>>"/bbdb"))
#+END_SRC

#+RESULTS: bbdb-dir
: "~/.config/emacs-common/bbdb"

Mu4e configuration.
#+BEGIN_SRC emacs-lisp :eval never :noweb yes :tangle ./modules/email.el
    (add-to-list 'load-path "/usr/share/emacs/site-lisp/mu4e")
    (require 'mu4e)

    (setq mu4e-maildir <<yahoo-email-dir()>>) ;; or wherever your Maildir lives
    (require 'mu4e)

    (setq user-mail-address <<yahoo-email-address()>>)
    (setq user-full-name <<yahoo-email-fullname()>>)

    (setq send-mail-function 'sendmail-send-it
          message-send-mail-function 'sendmail-send-it
          sendmail-program "/usr/bin/msmtp"
          mail-specify-envelope-from t
          mail-envelope-from 'header)

    (defun my/run-mbsync ()
      "Run mbsync to sync mail."
      (start-process-shell-command "mbsync" "*mbsync*" "mbsync -a"))

    ;; Run every 5 minutes (adjust as needed)
    (run-at-time "5 min" 300 #'my/run-mbsync)

    (setq mu4e-update-interval 300)  ;; 5 minutes

    ;; Setup image preview
    (setq mu4e-view-show-images t)
    (setq mu4e-view-use-gnus t) 
    (setq mu4e-view-image-max-width 800)
    (setq mu4e-view-show-addresses 't)

    (setq shr-inhibit-images nil)
    (setq gnus-inhibit-images nil)

    (defun my-mu4e-view-inline-images ()
      "Show images automatically in mu4e."
      (when (fboundp 'shr-put-image)
        (setq mu4e-view-show-images t)))

    (setq url-privacy-level 'low)

    (defun my/mu4e-view-message-no-focus ()
      "View the current message in another window without changing focus."
      (interactive)
      (let ((msg (mu4e-message-at-point)))
        (when msg
          (save-selected-window
            (mu4e-view msg)))))

    (with-eval-after-load 'mu4e
      (define-key mu4e-headers-mode-map (kbd "V") #'my/mu4e-view-message-no-focus))

    ;; Open email in a dedicated frame for better workflow.
    (defun my/mu4e-open-in-dedicated-frame ()
      "Open mu4e in a dedicated frame named 'mu4e'."
      (interactive)
      (let ((bufname "*mu4e*"))
        (if (get-buffer bufname)
            ;; If buffer already exists, raise the frame or switch to it
            (progn
              (select-frame-set-input-focus
               (window-frame (get-buffer-window bufname))))
          ;; Else create new frame and launch mu4e
  	(let* ((frame (make-frame '((name . "mu4e")
                                      (width . 100)
                                      (height . 40)))))
            (select-frame-set-input-focus frame)
            (with-selected-frame frame
              (mu4e)
              (set-window-dedicated-p (selected-window) t))))))

    ;; Use bbdb for email contacts configuration.
    (use-package bbdb
          :defer t )

    (setq bbdb-file <<bbdb-dir()>>)
    (require 'bbdb)
    (require 'bbdb-com)
    (bbdb-initialize 'mu4e 'message)

    (setq mu4e-use-bbdb t)

    (bbdb-mua-auto-update-init 'mu4e)

    (setq message-completion-alist
        '((message-to . bbdb-complete-mail)
          (message-cc . bbdb-complete-mail)
          (message-bcc . bbdb-complete-mail)))

    (define-key message-mode-map (kbd "TAB") 'bbdb-complete-mail)
 #+END_SRC

* Dev Environment

My dev envrionments.
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/dev.el
  (use-package typescript-mode
    :mode "\\.ts\\'")

  (use-package python-mode
    :hook (python-mode . eglot-ensure))

  (use-package pyvenv
    :config (pyvenv-mode 1))

  (use-package projectile
    :diminish projectile-mode
    :config (projectile-mode)
    :custom ((projectile-completion-system 'ivy))
    :bind-keymap
    ("C-c p" . projectile-command-map)
    :init
    (when (file-directory-p "~/Documents/Code")
      (setq projectile-project-search-path '("~/Documents/Code")))
    (setq projectile-switch-project-action #'projectile-dired))

  (use-package counsel-projectile
    :config (counsel-projectile-mode))

  (use-package rainbow-delimiters
    :hook (prog-mode . rainbow-delimiters-mode))

  (use-package request
  :ensure t)

  (require 'request)
  (require 'json)

  ;; Git integration.
  (use-package magit
    :config
    (setq magit-push-always-verify nil)
    (setq git-commit-summary-max-length 50)
    :bind
    ("M-g" . magit-status))

  (use-package treemacs-magit
    :after treemacs magit)

  (use-package ghub
    :demand t
    :after magit)  

  ;; Enable Eglot automatically for certain modes
  (add-hook 'python-mode-hook #'eglot-ensure)

#+END_SRC

* AI

My AI envrionments.
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/ai.el
    ;; ChatGPT AI integration.
  (use-package chatgpt-shell
    :ensure t
    :config
    (setq chatgpt-shell-save-session t)
    (global-set-key (kbd "C-c g") #'chatgpt-shell)
    (setq chatgpt-shell-openai-key my-openai-api-key)
    (setq chatgpt-shell-anthropic-key my-anthropic-api-key)
    (setq chatgpt-shell-google-key my-gemini-api-key))

  (use-package ollama-buddy
    :ensure t
    :commands (ollama-buddy-chat ollama-buddy-prompt-region ollama-buddy-prompt-buffer)
    :config)
        
#+END_SRC

* My Functions

This section is dedicated to my custom functions. 

Niri literate config.kdl tangle and deploy. This function will evaluate
and tangle the niri config.kdl and deploy it into the correct niri
directory, with included rollback capability.
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/my-functions.el
(defun niri-babel-build-and-deploy ()
  "Tangle and deploy config.kdl to ~/.config/niri/config.kdl with 5 rotating backups.
Also copy key_bindings.txt to ~/.config/niri/ if present."
  (interactive)
  (let* ((org-file   "~/projects/niri_babel_config/niri_config.org")
         (output-dir "~/projects/niri_babel_config/")
         (output-file (expand-file-name "config.kdl" output-dir))
         (kb-src      (expand-file-name "key_bindings.txt" output-dir))
         (target-dir  (expand-file-name "~/.config/niri/"))
         (target-file (expand-file-name "config.kdl" target-dir))
         (kb-target   (expand-file-name "key_bindings.txt" target-dir)))

    ;; Execute all non-KDL blocks first
    (with-current-buffer (find-file-noselect org-file)
      (org-babel-map-src-blocks org-file
        (let* ((info (org-babel-get-src-block-info 'light))
               (lang (nth 0 info)))
          (unless (string= lang "kdl")
            (org-babel-execute-src-block))))
      ;; Tangle everything
      (org-babel-tangle))

    ;; Ensure target directory exists
    (make-directory target-dir t)

    ;; Backup rotation (keep last 5) for config.kdl
    (when (file-exists-p target-file)
      (dotimes (i 5)
        (let* ((n (- 5 i))
               (old (format "%s.%03d" target-file n))
               (new (format "%s.%03d" target-file (1+ n))))
          (when (file-exists-p old)
            (rename-file old new t))))
      (copy-file target-file (format "%s.001" target-file) t))

    ;; Deploy new config.kdl
    (when (file-exists-p output-file)
      (copy-file output-file target-file t)
      (message "Tangled and deployed config.kdl to %s" target-file))

    ;; Copy key_bindings.txt if present
    (when (file-exists-p kb-src)
      (copy-file kb-src kb-target t)
      (message "Copied key_bindings.txt to %s" kb-target))))
#+END_SRC

Deploy emacs configurations and mark deployment date.
#+BEGIN_SRC emacs-lisp :eval never :noweb yes :tangle ./modules/my-functions.el
(defun emacs-babel-build-and-deploy ()
  "Tangle and deploy Emacs config to proper env directory with backup and timestamp."
  (interactive)
  (let* ((target-env <<target-env()>>)  ;; Assumes `target-env` is a custom function
         (org-file "~/projects/emacs_babel_config/emacs_config.org")
         (modules-dir (expand-file-name (format "~/.config/%s/modules" target-env)))
         (src-dir (expand-file-name "~/projects/emacs_babel_config/modules/"))
         (timestamp-file (expand-file-name
                          (format "~/.config/%s/last_deployed.org" target-env))))

    ;; Debug
    (message "Target env: %s" target-env)

    ;; Run non-Elisp blocks to update values
    (with-current-buffer (find-file-noselect org-file)
      (org-babel-map-src-blocks org-file
        (let* ((info (org-babel-get-src-block-info 'light)))
          (when info
            (let ((lang (nth 0 info)))
              (unless (string= lang "emacs-lisp")
                (org-babel-execute-src-block))))))

    ;; Tangle all blocks
    (org-babel-tangle)

    ;; Copy modules
    (when (file-directory-p src-dir)
      (make-directory modules-dir t)
      (dolist (file (directory-files src-dir t "^[^.].*"))  ; skip dotfiles
        (copy-file file
                   (expand-file-name (file-name-nondirectory file) modules-dir)
                   t)))

    ;; Write timestamp
    (with-temp-file timestamp-file
      (insert (format "* Last Deployed\n\nDeployed at: %s\n" (current-time-string))))

    (message "Emacs config deployed to %s" modules-dir))))
#+END_SRC

This is a prototype / test / learning function. To use it add this function to
emacsclient startup and it will display thumbnails for the screenshots directory,
allow you to select a single thumbnail, copy it to the clipboard and exit.
This is used in niri with a similar function using feh. The goal was to see
how close emacs could reproduce the feh functionality. The results are pretty
good and this is currently wired as a keybind in niri as is feh.
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/my-functions.el
     (require 'image-dired)

     (defun my/image-dired-copy-and-exit ()
       "Copy image under point in image-dired and exit Emacsclient."
       (interactive)
       (let* ((file (image-dired-original-file-name))
              (copy-prog (or (executable-find "wl-copy")
                             (executable-find "xclip"))))
         (unless copy-prog
           (error "No clipboard utility (wl-copy or xclip) found"))
         (unless (and file (file-exists-p file))
           (error "No image found under cursor"))
         (with-temp-buffer
           (insert-file-contents-literally file)
           (call-process-region
            (point-min) (point-max)
            copy-prog nil nil nil "-t" "image/png"))
         (save-buffers-kill-terminal)))

     (with-eval-after-load 'image-dired
       ;; `m` to copy and exit
       (define-key image-dired-thumbnail-mode-map (kbd "m") #'my/image-dired-copy-and-exit)
       ;; `q` to just quit
       (define-key image-dired-thumbnail-mode-map (kbd "q")
         (lambda ()
           (interactive)
           (save-buffers-kill-terminal))))

     (defun my/image-picker-thumbnail-mode ()
       "Launch thumbnail-only image picker. Press `m` to copy & exit."
       (interactive)
       (let ((dir "~/Pictures/screenshots/"))
         ;; Save current window configuration, run image-dired
         (image-dired dir)
         ;; Force delete all windows except the one showing *image-dired*
         (let ((image-buffer "*image-dired*"))
           (dolist (win (window-list))
             (unless (eq (window-buffer win) (get-buffer image-buffer))
               (delete-window win)))
           (select-window (get-buffer-window image-buffer)))))
 #+END_SRC

Show the server name that this emacsclient is connected to.
 #+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/my-functions.el
    ;; Show the server name that this emacsclient is connected to.
    (defun show-current-server-name ()
      "Display the name of the Emacs server this client is connected to."
      (interactive)
      (message "Connected to Emacs server: %s" server-name))

    ;; Then bind it in the startup hook
    (add-hook 'emacs-startup-hook
              (lambda ()
                (global-set-key (kbd "<f12>") #'show-current-server-name)))
 #+END_SRC

List niri active windows.
 #+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/my-functions.el
   ;; Output niri-windows to new buffer
   (defun niri-windows ()
     "Show Niri windows in a new buffer."
     (interactive)
     (let ((buf (get-buffer-create "*niri-windows*")))
       (with-current-buffer buf
         (read-only-mode -1)
         (erase-buffer)
         (call-process "~/projects/niri_toolkit/niri-windows.py" nil buf)
         (goto-char (point-min))
         (read-only-mode 1))
       (pop-to-buffer buf)))
 #+END_SRC

Connect to niri IPC and display events in a buffer.
  #+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/my-functions.el
   ;;Output niri-event-stream via IPC to new buffer
   (defun niri-event-stream ()
     "Show the Niri event stream in a new buffer."
     (interactive)
     (let ((buf (get-buffer-create "*Niri Event Stream*")))
       (apply 'make-comint-in-buffer
              "Niri Event Stream"
              buf
              (expand-file-name "~/projects/niri_toolkit/niri-tail-event-stream.py")
              nil)
       (pop-to-buffer buf)))
 #+END_SRC

Mount my timeshift backup and dir.
To use this execute the following commands from cli:
 sudo mkdir -p /mnt/timeshift
 sudo mount -o ro /dev/sdb1 /mnt/timeshift
#+BEGIN_SRC emacs-lisp :eval never :tangle ./modules/my-functions.el
(defun open-timeshift-backup ()
  "Open already-mounted Timeshift backup in Dired."
  (interactive)
  (let ((mount-point "/mnt/timeshift"))
    (if (file-directory-p mount-point)
        (dired mount-point)
      (message "Mount point does not exist or is not accessible: %s" mount-point))))
 #+END_SRC    
